HRC400DS - HI, TS, TE Immediate Commmands.
Thanks to Bob Bolch - May 2020

Two bits in memory page zero are set or reset by these immediate commands
At address X'5E6',  the tracing bit is X'01; and the Halt Interpretation bit is X'10'.
You can see the bits any time with command 'D 5e6'. Example:

  d 5e6

0005E4    00000140

The X'01' bit shows that EXEC tracing is in effect.

The EXECTRAC program mimics the SET EXECTRAC ON\OFF command
from later VM systems. Putting them into SET means putting them into QUERY
and it is just easier to have an EXECTRAC ON\OFF  program.  The command is
used in later VM systems to start tracing before the EXEC is called, rather than
starting the EXEC and trying to type TS really quickly ;-)

Usage Examples

CHECKHI ASSEMBLE - checks and rests HI

CHECKHI  CSECT
         STM   R14,12,12(R13)
         LR    R12,R15
         USING CHECKHI,R12
         USING NUCON,0
*
         SR    R15,R15
         TM    EXECFLAG,EXECHALT  HI ISSUED?
         BZ    NOTYET             BIF NOT
         IPK   ,                  GET CURRENT PROTECT KEY
         SPKA  0
         SSM   *+1
         NI    EXECFLAG,X'FF'-EXECHALT RESET
         SPKA  0(R2)
         SSM   =X'FF'
         LA    R15,1
NOTYET   DS    0H
         L     R14,12(,R13)
         LM    R0,R12,20(R13)
         BR    R14
         REGEQU ,
         NUCON ,
         END


EXECTRAC ASSEMBLE - Set and Reset Trace

EXTRACE  CSECT
         STM   R14,12,12(R13)
         LR    R12,R15
         USING EXTRACE,R12
         USING NUCON,0
*
         SR    R15,R15
         CLC   8(8,R1),=CL8'ON'   Turn on tracing?
         BE    EXTON              Bif so
         CLC   8(8,R1),=CL8'OFF'  Turn off tracing
         BE    EXTOFF             Bif so
*
         LINEDIT TEXT='EXECTRAC syntax is EXECTRAC ON!OFF'
         LA    R15,16
         B     DONE
*
EXTON    DS    0H
         IPK   ,
         SPKA  0
         SSM   *+1
          OI    EXECFLAG,EXECTRAC       SET EXECTRAC On
         B     EXRESET
*
EXTOFF   DS    0H
         IPK   ,
         SPKA  0
         SSM   *+1
         NI    EXECFLAG,X'FF'-EXECTRAC      Set EXECTRAC off
EXRESET  DS    0H
         SPKA  0(R2)
         SSM   =X'FF'
*
DONE     DS    0H
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BR    R14
         EJECT ,
         REGEQU ,
         NUCON ,
         END
